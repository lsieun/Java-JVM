# Opcode Constants

<!-- TOC -->

- [1. int](#1-int)
  - [1.1. iconst](#11-iconst)
  - [1.2. bipush](#12-bipush)
  - [1.3. sipush](#13-sipush)
  - [1.4. ldc](#14-ldc)
  - [1.5. ldc_w](#15-ldcw)
- [2. long](#2-long)
  - [2.1. lconst](#21-lconst)
  - [2.2. ldc2_w](#22-ldc2w)
- [3. float and double](#3-float-and-double)
  - [3.1. fconst](#31-fconst)
  - [3.2. dconst](#32-dconst)
- [4. null](#4-null)
- [5. Summing-up](#5-summing-up)

<!-- /TOC -->

**Constants**. These instructions push a constant value on the operand stack: `ACONST_NULL` pushes `null`, `ICONST_0` pushes the `int` value `0`, `FCONST_0` pushes `0f`, `DCONST_0` pushes `0d`, `BIPUSH b` pushes the `byte` value `b`, `SIPUSH s` pushes the `short` value `s`, `LDC cst` pushes the arbitrary `int`, `float`, `long`, `double`, `String`, or `class` constant `cst`, etc.

## 1. int

### 1.1. iconst

学习目标：

- `iconst_<n>`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m1 = -1;
        int int_0 = 0;
        int int_1 = 1;
        int int_2 = 2;
        int int_3 = 3;
        int int_4 = 4;
        int int_5 = 5;
    }
}
```

输出结果：

```txt
code='023C033D043E053604063605073606083607B1'
    0: iconst_m1       // 02
    1: istore_1        // 3C
    2: iconst_0        // 03
    3: istore_2        // 3D
    4: iconst_1        // 04
    5: istore_3        // 3E
    6: iconst_2        // 05
    7: istore 4        // 3604
    9: iconst_3        // 06
   10: istore 5        // 3605
   12: iconst_4        // 07
   13: istore 6        // 3606
   15: iconst_5        // 08
   16: istore 7        // 3607
   18: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      19  this:Llsieun/sample/HelloWorld;
    1         2      17  int_m1:I
    2         4      15  int_0:I
    3         6      13  int_1:I
    4         9      10  int_2:I
    5        12       7  int_3:I
    6        15       4  int_4:I
    7        18       1  int_5:I
```

### 1.2. bipush

学习目标：

- `bipush`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m128 = -128;
        int int_m2 = -2;
        int int_6 = 6;
        int int_127 = 127;
    }
}
```

输出结果：

```txt
code='10803C10FE3D10063E107F3604B1'
    0: bipush 128      // 1080
    2: istore_1        // 3C
    3: bipush 254      // 10FE
    5: istore_2        // 3D
    6: bipush 6        // 1006
    8: istore_3        // 3E
    9: bipush 127      // 107F
   11: istore 4        // 3604
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         3      11  int_m128:I
    2         6       8  int_m2:I
    3         9       5  int_6:I
    4        13       1  int_127:I
```

### 1.3. sipush

学习目标：

- `sipush`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m32768 = -32768;
        int int_m129 = -129;
        int int_128 = 128;
        int int_32767 = 32767;
    }
}
```

输出结果：

```txt
code='1180003C11FF7F3D1100803E117FFF3604B1'
    0: sipush 32768    // 118000
    3: istore_1        // 3C
    4: sipush 65407    // 11FF7F
    7: istore_2        // 3D
    8: sipush 128      // 110080
   11: istore_3        // 3E
   12: sipush 32767    // 117FFF
   15: istore 4        // 3604
   17: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      18  this:Llsieun/sample/HelloWorld;
    1         4      14  int_m32768:I
    2         8      10  int_m129:I
    3        12       6  int_128:I
    4        17       1  int_32767:I
```

### 1.4. ldc

学习目标：

- `ldc`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_min = Integer.MIN_VALUE;
        int int_m32789 = -32769;
        int int_32768 = 32768;
        int int_max = Integer.MAX_VALUE;
    }
}
```

输出结果：

```txt
ConstantPool {
    |003| CONSTANT_Integer {Value='-2147483648', HexCode='0380000000'}
    |004| CONSTANT_Integer {Value='-32769', HexCode='03FFFF7FFF'}
    |005| CONSTANT_Integer {Value='32768', HexCode='0300008000'}
    |006| CONSTANT_Integer {Value='2147483647', HexCode='037FFFFFFF'}
    ......

code='12033C12043D12053E12063604B1'
    0: ldc 3           // 1203
    2: istore_1        // 3C
    3: ldc 4           // 1204
    5: istore_2        // 3D
    6: ldc 5           // 1205
    8: istore_3        // 3E
    9: ldc 6           // 1206
   11: istore 4        // 3604
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         3      11  int_min:I
    2         6       8  int_m32789:I
    3         9       5  int_32768:I
    4        13       1  int_max:I
```

### 1.5. ldc_w

```java
public void testSimple() {
    String str = "str258";
}

public static void main(String[] args) {
    String format = "private String str%03d = \"str%03d\";";
    for(int i=1; i<258; i++) {
        String line = String.format(format, i,i);
        System.out.println(line);
    }
}
```

## 2. long

### 2.1. lconst

学习目标：

- `lconst_<l>`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        long long_0 = 0;
        long long_1 = 1;
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Long {Value='2', HexCode='050000000000000002'}
    |004| CONSTANT_Class {Value='java/lang/Long', HexCode='070017'}
    |005| CONSTANT_Long {Value='9223372036854775807', HexCode='057FFFFFFFFFFFFFFF'}
    ......

code='09400A42B1'
    0: lconst_0        // 09
    1: lstore_1        // 40
    2: lconst_1        // 0A
    3: lstore_3        // 42
    4: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         2       3  long_0:J
    3         4       1  long_1:J
```

### 2.2. ldc2_w

学习目标：

- `ldc2_w`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        long long_2 = 2;
        long long_max = Long.MAX_VALUE;
    }
}
```

输出结果：

```txt
code='1400024014000542B1'
    0: ldc2_w 2        // 140002
    3: lstore_1        // 40
    4: ldc2_w 5        // 140005
    7: lstore_3        // 42
    8: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       9  this:Llsieun/sample/HelloWorld;
    1         4       5  long_2:J
    3         8       1  long_max:J
```

## 3. float and double

### 3.1. fconst

学习目标：

- `fconst_<f>`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        float float_0 = 0;
        float float_1 = 1;
        float float_2 = 2;
        float float_PI = 3.14F;
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Float {Value='3.14', HexCode='044048F5C3'}
    ......

code='0B440C450D4612023804B1'
    0: fconst_0        // 0B
    1: fstore_1        // 44
    2: fconst_1        // 0C
    3: fstore_2        // 45
    4: fconst_2        // 0D
    5: fstore_3        // 46
    6: ldc 2           // 1202
    8: fstore 4        // 3804
   10: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      11  this:Llsieun/sample/HelloWorld;
    1         2       9  float_0:F
    2         4       7  float_1:F
    3         6       5  float_2:F
    4        10       1  float_PI:F
```

### 3.2. dconst

学习目标：

- `dconst_<d>`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        double double_0 = 0;
        double double_1 = 1;
        double double_PI = 3.14D;
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Double {Value='3.14', HexCode='0640091EB851EB851F'}
    |004| CONSTANT_Class {Value='lsieun/sample/HelloWorld', HexCode='070015'}
    ......

code='0E480F4A1400023905B1'
    0: dconst_0        // 0E
    1: dstore_1        // 48
    2: dconst_1        // 0F
    3: dstore_3        // 4A
    4: ldc2_w 2        // 140002
    7: dstore 5        // 3905
    9: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      10  this:Llsieun/sample/HelloWorld;
    1         2       8  double_0:D
    3         4       6  double_1:D
    5         9       1  double_PI:D
```

## 4. null

学习目标：

- `aconst_null`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object obj_null = null;
        String str_null = null;
        String str_en = "Welcome";
        String str_zh = "欢迎";
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_String {Value='Welcome', HexCode='080017'}
    |003| CONSTANT_String {Value='欢迎', HexCode='080018'}
    ......

code='014C014D12024E12033A04B1'
    0: aconst_null     // 01
    1: astore_1        // 4C
    2: aconst_null     // 01
    3: astore_2        // 4D
    4: ldc 2           // 1202
    6: astore_3        // 4E
    7: ldc 3           // 1203
    9: astore 4        // 3A04
   11: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      12  this:Llsieun/sample/HelloWorld;
    1         2      10  obj_null:Ljava/lang/Object;
    2         4       8  str_null:Ljava/lang/String;
    3         7       5  str_en:Ljava/lang/String;
    4        11       1  str_zh:Ljava/lang/String;
```

## 5. Summing-up

知识总结（一）：`int`的取值

| 取值范围             | opcode mnemonic symbol |
| -------------------- | ---------------------- |
| `-2147483648~-32769` | `ldc`                  |
| `-32768~-129`        | `sipush`               |
| `-128~-2`            | `bipush`               |
| `-1~5`               | `iconst_<n>`           |
| `6~127`              | `bipush`               |
| `128~32767`          | `sipush`               |
| `32768~2147483647`   | `ldc`                  |

知识总结（二）

| opcode | mnemonic symbol | meaning                                                      |
| ------ | --------------- | ------------------------------------------------------------ |
| `1`    | `aconst_null`   | Push `null`                                                  |
| `2`    | `iconst_m1`     | Push `int` constant                                          |
| `3`    | `iconst_0`      |                                                              |
| `4`    | `iconst_1`      |                                                              |
| `5`    | `iconst_2`      |                                                              |
| `6`    | `iconst_3`      |                                                              |
| `7`    | `iconst_4`      |                                                              |
| `8`    | `iconst_5`      |                                                              |
| `9`    | `lconst_0`      | Push `long` constant                                         |
| `10`   | `lconst_1`      |                                                              |
| `11`   | `fconst_0`      | Push `float`                                                 |
| `12`   | `fconst_1`      |                                                              |
| `13`   | `fconst_2`      |                                                              |
| `14`   | `dconst_0`      | Push `double`                                                |
| `15`   | `dconst_1`      |                                                              |
| `16`   | `bipush`        | Push `byte`                                                  |
| `17`   | `sipush`        | Push `short`                                                 |
| `18`   | `ldc`           | Push item from run-time constant pool                        |
| `19`   | `ldc_w`:bug:    | Push item from run-time constant pool (wide index)           |
| `20`   | `ldc2_w`        | Push `long` or `double` from run-time constant pool(wide index) |

