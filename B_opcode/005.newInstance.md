# 创建对象

<!-- TOC -->

- [1. String](#1-string)
  - [1.1. String null](#11-string-null)
  - [1.2. String constant pool](#12-string-constant-pool)
  - [1.3. String object](#13-string-object)
- [2. Object](#2-object)
  - [2.1. Object null](#21-object-null)
  - [2.2. new Object](#22-new-object)
- [3. array](#3-array)
  - [3.1. byte array](#31-byte-array)
  - [3.2. int array](#32-int-array)
  - [3.3. String array](#33-string-array)
  - [3.4. Object Array](#34-object-array)
- [4. 小结](#4-%E5%B0%8F%E7%BB%93)

<!-- /TOC -->

## 1. String

### 1.1. String null

学习目标

- `aconst_null`: Push `null`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        String strValue = null;
    }
}
```

输出结果：

```txt
code='014CB1'
    0: aconst_null     // 01
    1: astore_1        // 4C
    2: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       3  this:Llsieun/sample/HelloWorld;
    1         2       1  strValue:Ljava/lang/String;
```

### 1.2. String constant pool

学习目标：

- `ldc index`: Push item from run-time constant pool
- `astore_<n>`: Store `reference` into local variable
- `astore index`: Store `reference` into local variable

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        String strA = "Atmosphere";
        String strB = "Blue";
        String strC = "Courageous";
        String strD = "Duck";
        String strE = "Express";
        String strF = "Fruit";
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_String {Value='Atmosphere', HexCode='08001C'}
    |003| CONSTANT_String {Value='Blue', HexCode='08001D'}
    |004| CONSTANT_String {Value='Courageous', HexCode='08001E'}
    |005| CONSTANT_String {Value='Duck', HexCode='08001F'}
    |006| CONSTANT_String {Value='Express', HexCode='080020'}
    |007| CONSTANT_String {Value='Fruit', HexCode='080021'}
    ......

code='12024C12034D12044E12053A0412063A0512073A06B1'
    0: ldc 2           // 1202
    2: astore_1        // 4C
    3: ldc 3           // 1203
    5: astore_2        // 4D
    6: ldc 4           // 1204
    8: astore_3        // 4E
    9: ldc 5           // 1205
   11: astore 4        // 3A04
   13: ldc 6           // 1206
   15: astore 5        // 3A05
   17: ldc 7           // 1207
   19: astore 6        // 3A06
   21: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      22  this:Llsieun/sample/HelloWorld;
    1         3      19  strA:Ljava/lang/String;
    2         6      16  strB:Ljava/lang/String;
    3         9      13  strC:Ljava/lang/String;
    4        13       9  strD:Ljava/lang/String;
    5        17       5  strE:Ljava/lang/String;
    6        21       1  strF:Ljava/lang/String;
```

知识整理

| Format     | Decimal(十进制) | Hex(十六进制) | Operation                             |
| ---------- | --------------- | ------------- | ------------------------------------- |
| `ldc`      | `18`            | `0x12`        | Push item from run-time constant pool |
| `astore_0` | `75`            | `0x4b`        | Store `reference` into local variable |
| `astore_1` | `76`            | `0x4c`        |                                       |
| `astore_2` | `77`            | `0x4d`        |                                       |
| `astore_3` | `78`            | `0x4e`        |                                       |
| `astore`   | `58`            | `0x3a`        | Store `reference` into local variable |

### 1.3. String object

学习目标：

- `new indexbyte1 indexbyte2`: Create new object
- `dup`: Duplicate the top operand stack value
- `invokespecial indexbyte1 indexbyte2`: Invoke instance method; direct invocation of instance initialization methods and methods of the current class and its supertypes

示例代码：

```txt
public class HelloWorld {
    public void testSimple() {
        String strObject = new String();
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/String', HexCode='070013'}
    |003| CONSTANT_Methodref {Value='java/lang/String.<init>:()V', HexCode='0A00020012'}
    ......

code='BB000259B700034CB1'
    0: new 2           // BB0002
    3: dup             // 59
    4: invokespecial 3 // B70003
    7: astore_1        // 4C
    8: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       9  this:Llsieun/sample/HelloWorld;
    1         8       1  strObject:Ljava/lang/String;
```

## 2. Object

### 2.1. Object null

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object obj = null;
    }
}
```

输出结果：

```txt
code='014CB1'
    0: aconst_null     // 01
    1: astore_1        // 4C
    2: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       3  this:Llsieun/sample/HelloWorld;
    1         2       1  obj:Ljava/lang/Object;
```

### 2.2. new Object

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object obj = new Object();
    }
}
```

输出结果：

```txt
ConstantPool {
    |001| CONSTANT_Methodref {Value='java/lang/Object.<init>:()V', HexCode='0A00020010'}
    |002| CONSTANT_Class {Value='java/lang/Object', HexCode='070011'}
    ......

code='BB000259B700014CB1'
    0: new 2           // BB0002
    3: dup             // 59
    4: invokespecial 1 // B70001
    7: astore_1        // 4C
    8: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       9  this:Llsieun/sample/HelloWorld;
    1         8       1  obj:Ljava/lang/Object;
```

## 3. array

### 3.1. byte array

学习目标：

- `newarray atype`: Create new array.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        byte[] byte_array = new byte[2];
    }
}
```

输出结果：

```txt
code='05BC084CB1'
    0: iconst_2        // 05
    1: newarray 8      // BC08
    3: astore_1        // 4C
    4: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         4       1  byte_array:[B
```

知识整理

| Format           | Decimal | Hex    | Operation        |
| ---------------- | ------- | ------ | ---------------- |
| `newarray atype` | `188`   | `0xbc` | Create new array |


The `atype` is a code that indicates the type of array to create. It must take one of the following values:

| Array Type  | atype |
| ----------- | ----- |
| `T_BOOLEAN` | `4`   |
| `T_CHAR`    | `5`   |
| `T_FLOAT`   | `6`   |
| `T_DOUBLE`  | `7`   |
| `T_BYTE`    | `8`   |
| `T_SHORT`   | `9`   |
| `T_INT`     | `10`  |
| `T_LONG`    | `11`  |


### 3.2. int array

学习目标：

- `newarray atype`: Create new array.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int[] int_array = new int[0];
    }
}
```

输出结果：

```txt
code='03BC0A4CB1'
    0: iconst_0        // 03
    1: newarray 10     // BC0A
    3: astore_1        // 4C
    4: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         4       1  int_array:[I
```

### 3.3. String array

学习目标：

- `anewarray indexbyte1 indexbyte2`: Create new array of `reference`.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        String[] str_array = new String[5];
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/String', HexCode='070012'}
    ......

code='08BD00024CB1'
    0: iconst_5        // 08
    1: anewarray 2     // BD0002
    4: astore_1        // 4C
    5: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       6  this:Llsieun/sample/HelloWorld;
    1         5       1  str_array:[Ljava/lang/String;
```

学习目标：

- `aastore`: Store into `reference` array

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        String[] str_array = new String[]{"ABC", "XYZ"};
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/String', HexCode='070014'}
    |003| CONSTANT_String {Value='ABC', HexCode='080015'}
    |004| CONSTANT_String {Value='XYZ', HexCode='080016'}
    ......

code='05BD0002590312035359041204534CB1'
    0: iconst_2        // 05
    1: anewarray 2     // BD0002
    4: dup             // 59
    5: iconst_0        // 03
    6: ldc 3           // 1203
    8: aastore         // 53
    9: dup             // 59
   10: iconst_1        // 04
   11: ldc 4           // 1204
   13: aastore         // 53
   14: astore_1        // 4C
   15: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      16  this:Llsieun/sample/HelloWorld;
    1        15       1  str_array:[Ljava/lang/String;
```

示例代码

```java
public class HelloWorld {
    public void testSimple() {
        String[] str_array = new String[2];
        str_array[0] = "ABC";
        str_array[1] = "XYZ";
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/String', HexCode='070014'}
    |003| CONSTANT_String {Value='ABC', HexCode='080015'}
    |004| CONSTANT_String {Value='XYZ', HexCode='080016'}
    ......

code='05BD00024C2B031203532B04120453B1'
    0: iconst_2        // 05
    1: anewarray 2     // BD0002
    4: astore_1        // 4C
    5: aload_1         // 2B
    6: iconst_0        // 03
    7: ldc 3           // 1203
    9: aastore         // 53
   10: aload_1         // 2B
   11: iconst_1        // 04
   12: ldc 4           // 1204
   14: aastore         // 53
   15: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      16  this:Llsieun/sample/HelloWorld;
    1         5      11  str_array:[Ljava/lang/String;
```

### 3.4. Object Array

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object[] obj_array = new Object[10];
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/Object', HexCode='070011'}
    ......

code='100ABD00024CB1'
    0: bipush 10       // 100A
    2: anewarray 2     // BD0002
    5: astore_1        // 4C
    6: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       7  this:Llsieun/sample/HelloWorld;
    1         6       1  obj_array:[Ljava/lang/Object;
```

## 4. 小结


