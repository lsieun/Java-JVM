# 002、变量

The JVM is both a **stack machine** and a **register machine**. Each frame for a method call has an "**operand stack**" and an array of "**local variables**". **The operand stack** is used for operands to computations and for receiving the return value of a called method, while **local variables** serve the same purpose as registers and are also used to pass method arguments.

要注意method内部涉及到的两个存储数据的结构：operand stack和local variables array。

opcode的操作数的三个来源：数（真实数据）、常量池（索引）、operand stack（索引）。

Boolean
byte
short
Integer
Long
Float
double
装箱、拆箱
String
Object
byte array
int array
string array
object array
add

<!-- TOC -->

- [1. Nothing](#1-nothing)
- [2. Integer](#2-integer)
  - [2.1. Integer Range: `-1~5`](#21-integer-range--15)
  - [2.2. Integer Range: `-128~-2` and `6~127`](#22-integer-range--128-2-and-6127)
  - [2.3. Integer Range: `-32768~-129` and `128~32767`](#23-integer-range--32768-129-and-12832767)
  - [2.4. Integer Range: `-2147483648~-32769` and `32768~2147483647`](#24-integer-range--2147483648-32769-and-327682147483647)
- [3. Boolean](#3-boolean)
- [4. Byte](#4-byte)
  - [4.1. Byte Range: `-1~5`](#41-byte-range--15)
  - [4.2. Byte Range: `-128~-2` and `6~127`](#42-byte-range--128-2-and-6127)
- [5. Short](#5-short)
  - [5.1. Short Range: `-1~5`](#51-short-range--15)
  - [5.2. Short Rang: `-128~-2` and `6~127`](#52-short-rang--128-2-and-6127)
  - [5.3. Short Rang: `-32768~-129` and `128~32767`](#53-short-rang--32768-129-and-12832767)
- [6. Long](#6-long)
  - [6.1. Long Range: `0` and `1`](#61-long-range-0-and-1)
  - [6.2. Long Range: Remaining](#62-long-range-remaining)
- [7. Float](#7-float)
  - [7.1. Float 001](#71-float-001)
  - [7.2. Float 002](#72-float-002)
- [8. Double](#8-double)
  - [8.1. Double 001](#81-double-001)
  - [8.2. Double 002](#82-double-002)
- [9. Boxing Conversion](#9-boxing-conversion)
  - [9.1. boolean to Boolean](#91-boolean-to-boolean)
  - [9.2. byte to Byte](#92-byte-to-byte)
  - [9.3. short to Short](#93-short-to-short)
  - [9.4. char to Character](#94-char-to-character)
  - [9.5. int to Integer](#95-int-to-integer)
  - [9.6. long to Long](#96-long-to-long)
  - [9.7. float to Float](#97-float-to-float)
  - [9.8. double to Double](#98-double-to-double)
- [10. Unboxing Conversion](#10-unboxing-conversion)
  - [10.1. Boolean to boolean](#101-boolean-to-boolean)
  - [10.2. Integer to int](#102-integer-to-int)
- [14. 小结](#14-%E5%B0%8F%E7%BB%93)

<!-- /TOC -->

三个事物： codes、stack、local variable array、constant pool(get)

byte[] codes = ...

对于`codes`分清三个概念：(1)索引、(2)值、(3)值的含义。

由“(1)索引”得到“(2)值”，再由opcode将“(2)值”衍生出“(3)值的含义”。

有一个opcode表

```txt
code='B1'
    0: return          // B1
    # 这里的0是指codes[]的索引

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       1  this:Llsieun/sample/HelloWorld;
```

## 1. Nothing

学习目标：

- `return`: Return `void` from method.

示例代码

```java
public class HelloWorld {
    public void testSimple() {
        // do nothing
    }
}
```

输出结果:

```txt
code='B1'
    0: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       1  this:Llsieun/sample/HelloWorld;
```

知识整理：

| Format   | Decimal(十进制) | Hex(十六进制) | Operation                 |
| -------- | --------------- | ------------- | ------------------------- |
| `return` | `177`           | `0xb1`        | Return `void` from method |

## 2. Integer

### 2.1. Integer Range: `-1~5`

学习目标：

- `iconst_<i>`: Push the `int` constant `<i>`(-1, 0, 1, 2, 3, 4, or 5) on to the operand stack.
- `istore_<n>`: Store `int` into local variable.
- `istore`: Store `int` into local variable.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m1 = -1;
        int int_0 = 0;
        int int_1 = 1;
        int int_2 = 2;
        int int_3 = 3;
        int int_4 = 4;
        int int_5 = 5;
    }
}
```

输出结果：

```txt
code='023C033D043E053604063605073606083607B1'
    0: iconst_m1       // 02
    1: istore_1        // 3C
    2: iconst_0        // 03
    3: istore_2        // 3D
    4: iconst_1        // 04
    5: istore_3        // 3E
    6: iconst_2        // 05
    7: istore 4        // 3604
    9: iconst_3        // 06
   10: istore 5        // 3605
   12: iconst_4        // 07
   13: istore 6        // 3606
   15: iconst_5        // 08
   16: istore 7        // 3607
   18: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      19  this:Llsieun/sample/HelloWorld;
    1         2      17  int_m1:I
    2         4      15  int_0:I
    3         6      13  int_1:I
    4         9      10  int_2:I
    5        12       7  int_3:I
    6        15       4  int_4:I
    7        18       1  int_5:I
```

知识整理

| Format           | Decimal(十进制) | Hex(十六进制) | Operation                       |
| ---------------- | --------------- | ------------- | ------------------------------- |
| `iconst_m1`      | `2`             | `0x02`        | Push `int` constant             |
| `iconst_0`       | `3`             | `0x03`        |                                 |
| `iconst_1`       | `4`             | `0x04`        |                                 |
| `iconst_2`       | `5`             | `0x05`        |                                 |
| `iconst_3`       | `6`             | `0x06`        |                                 |
| `iconst_4`       | `7`             | `0x07`        |                                 |
| `iconst_5`       | `8`             | `0x08`        |                                 |
| `istore_0`       | `59`            | `0x3b`        | Store `int` into local variable |
| `istore_1`       | `60`            | `0x3c`        |                                 |
| `istore_2`       | `61`            | `0x3d`        |                                 |
| `istore_3`       | `62`            | `0x3e`        |                                 |
| `istore` `index` | `54`            | `0x36`        | Store `int` into local variable |

这里应该有一个图

```txt
-1         0        1
iconst_m1 iconst_0 iconst_1
```

### 2.2. Integer Range: `-128~-2` and `6~127`

学习目标：

- `bipush`: Push `byte`. The immediate `byte` is sign-extended to an `int` value. That value is pushed on to the operand stack.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m128 = Byte.MIN_VALUE; // -128
        int int_m2 = -2;
        int int_6 = 6;
        int int_127 = Byte.MAX_VALUE; // 127
    }
}
```

输出结果：

```txt
code='10803C10FE3D10063E107F3604B1'
    0: bipush 128      // 1080
    2: istore_1        // 3C
    3: bipush 254      // 10FE
    5: istore_2        // 3D
    6: bipush 6        // 1006
    8: istore_3        // 3E
    9: bipush 127      // 107F
   11: istore 4        // 3604
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         3      11  int_m128:I
    2         6       8  int_m2:I
    3         9       5  int_6:I
    4        13       1  int_127:I
```

知识整理

| Format           | Decimal(十进制) | Hex(十六进制) | Operation   |
| ---------------- | --------------- | ------------- | ----------- |
| `bipush` `byte1` | `16`            | `0x10`        | Push `byte` |

### 2.3. Integer Range: `-32768~-129` and `128~32767`

学习目标：

- `sipush`: Push `short`.

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int int_m32768 = Short.MIN_VALUE; // -32768
        int int_m129 = -129;
        int int_128 = 128;
        int int_32767 = Short.MAX_VALUE; // 32767
    }
}
```

输出结果：

```txt
code='1180003C11FF7F3D1100803E117FFF3604B1'
    0: sipush 32768    // 118000
    3: istore_1        // 3C
    4: sipush 65407    // 11FF7F
    7: istore_2        // 3D
    8: sipush 128      // 110080
   11: istore_3        // 3E
   12: sipush 32767    // 117FFF
   15: istore 4        // 3604
   17: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      18  this:Llsieun/sample/HelloWorld;
    1         4      14  int_m32768:I
    2         8      10  int_m129:I
    3        12       6  int_128:I
    4        17       1  int_32767:I
```

知识整理

| Format                   | Decimal(十进制) | Hex(十六进制) | Operation    |
| ------------------------ | --------------- | ------------- | ------------ |
| `sipush` `byte1` `byte2` | `17`            | `0x11`        | Push `short` |

The immediate unsigned `byte1` and `byte2` values are assembled into an intermediate `short`, where the value of the `short` is `(byte1 << 8) | byte2`. The intermediate value is then sign-extended to an `int` value. That value is pushed onto the **operand stack**.

### 2.4. Integer Range: `-2147483648~-32769` and `32768~2147483647`

学习目标：

- `ldc`: Push item from run-time constant pool.

```java
public class HelloWorld {
    public void testSimple() {
        int int_min = Integer.MIN_VALUE; // -2147483648
        int int_m32769 = -32769;
        int int_32768 = 32768;
        int int_max = Integer.MAX_VALUE; // 2147483647
    }
}
```

```txt
ConstantPool {
    |003| CONSTANT_Integer {Value='-2147483648', HexCode='0380000000'}
    |004| CONSTANT_Integer {Value='-32769', HexCode='03FFFF7FFF'}
    |005| CONSTANT_Integer {Value='32768', HexCode='0300008000'}
    |006| CONSTANT_Integer {Value='2147483647', HexCode='037FFFFFFF'}
    ......

code='12033C12043D12053E12063604B1'
    0: ldc 3           // 1203
    2: istore_1        // 3C
    3: ldc 4           // 1204
    5: istore_2        // 3D
    6: ldc 5           // 1205
    8: istore_3        // 3E
    9: ldc 6           // 1206
   11: istore 4        // 3604
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         3      11  int_min:I
    2         6       8  int_m32769:I
    3         9       5  int_32768:I
    4        13       1  int_max:I
```

知识整理

| Format        | Decimal(十进制) | Hex(十六进制) | Operation                              |
| ------------- | --------------- | ------------- | -------------------------------------- |
| `ldc` `index` | `18`            | `0x12`        | Push item from run-time constant pool. |

## 3. Boolean

```java
public class HelloWorld {
    public void testSimple() {
        boolean flagTrue = true;
        boolean flagFalse = false;
    }
}
```

```txt
code='043C033DB1'
    0: iconst_1        // 04
    1: istore_1        // 3C
    2: iconst_0        // 03
    3: istore_2        // 3D
    4: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         2       3  flagTrue:Z
    2         4       1  flagFalse:Z
```

## 4. Byte

> 作者有话说：此部分，可略读。因为byte的取值范围是`-128～127`，这里涉及的内容，与Integer在`-128-127`之间采用的opcode是一样的。

### 4.1. Byte Range: `-1~5`

第一个例子是讲`iconst_<n>`

```java
public class HelloWorld {
    public void testSimple() {
        byte byte_m1 = -1;
        byte byte_0 = 0;
        byte byte_1 = 1;
        byte byte_2 = 2;
        byte byte_3 = 3;
        byte byte_4 = 4;
        byte byte_5 = 5;
    }
}
```

```txt
code='023C033D043E053604063605073606083607B1'
    0: iconst_m1       // 02
    1: istore_1        // 3C
    2: iconst_0        // 03
    3: istore_2        // 3D
    4: iconst_1        // 04
    5: istore_3        // 3E
    6: iconst_2        // 05
    7: istore 4        // 3604
    9: iconst_3        // 06
   10: istore 5        // 3605
   12: iconst_4        // 07
   13: istore 6        // 3606
   15: iconst_5        // 08
   16: istore 7        // 3607
   18: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      19  this:Llsieun/sample/HelloWorld;
    1         2      17  byte_m1:B
    2         4      15  byte_0:B
    3         6      13  byte_1:B
    4         9      10  byte_2:B
    5        12       7  byte_3:B
    6        15       4  byte_4:B
    7        18       1  byte_5:B
```

### 4.2. Byte Range: `-128~-2` and `6~127`

```java
public class HelloWorld {
    public void testSimple() {
        byte byte_m128 = Byte.MIN_VALUE;
        byte byte_m2 = -2;
        byte byte_6 = 6;
        byte byte_127 = 127;
    }
}
```

```txt
code='10803C10FE3D10063E107F3604B1'
    0: bipush 128      // 1080
    2: istore_1        // 3C
    3: bipush 254      // 10FE
    5: istore_2        // 3D
    6: bipush 6        // 1006
    8: istore_3        // 3E
    9: bipush 127      // 107F
   11: istore 4        // 3604
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         3      11  byte_m128:B
    2         6       8  byte_m2:B
    3         9       5  byte_6:B
    4        13       1  byte_127:B
```

## 5. Short

### 5.1. Short Range: `-1~5`

```java
public class HelloWorld {
    public void testSimple() {
        short short_m1 = -1;
        short short_0 = 0;
        short short_1 = 1;
        short short_2 = 2;
        short short_3 = 3;
        short short_4 = 4;
        short short_5 = 5;
    }
}
```

### 5.2. Short Rang: `-128~-2` and `6~127`

### 5.3. Short Rang: `-32768~-129` and `128~32767`

## 6. Long

### 6.1. Long Range: `0` and `1`

```java
public class HelloWorld {
    public void testSimple() {
        long long_0 = 0;
        long long_1 = 1;
    }
}
```

```txt
code='09400A42B1'
    0: lconst_0        // 09
    1: lstore_1        // 40
    2: lconst_1        // 0A
    3: lstore_3        // 42
    4: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         2       3  long_0:J
    3         4       1  long_1:J
```

### 6.2. Long Range: Remaining

```java
public class HelloWorld {
    public void testSimple() {
        long long_m1 = -1;
        long long_2 = 2;
        long long_min = Long.MIN_VALUE;
        long long_max = Long.MAX_VALUE;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Long {Value='-1', HexCode='05FFFFFFFFFFFFFFFF'}
    |004| CONSTANT_Long {Value='2', HexCode='050000000000000002'}
    |007| CONSTANT_Long {Value='-9223372036854775808', HexCode='058000000000000000'}
    |009| CONSTANT_Long {Value='9223372036854775807', HexCode='057FFFFFFFFFFFFFFF'}
    ......

code='140002401400044214000737051400093707B1'
    0: ldc2_w 2        // 140002
    3: lstore_1        // 40
    4: ldc2_w 4        // 140004
    7: lstore_3        // 42
    8: ldc2_w 7        // 140007
   11: lstore 5        // 3705
   13: ldc2_w 9        // 140009
   16: lstore 7        // 3707
   18: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      19  this:Llsieun/sample/HelloWorld;
    1         4      15  long_m1:J
    3         8      11  long_2:J
    5        13       6  long_min:J
    7        18       1  long_max:J
```

## 7. Float

### 7.1. Float 001

```java
public class HelloWorld {
    public void testSimple() {
        float float_0 = 0;
        float float_1 = 1;
        float float_2 = 2;
    }
}
```

```txt
code='0B440C450D46B1'
    0: fconst_0        // 0B
    1: fstore_1        // 44
    2: fconst_1        // 0C
    3: fstore_2        // 45
    4: fconst_2        // 0D
    5: fstore_3        // 46
    6: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       7  this:Llsieun/sample/HelloWorld;
    1         2       5  float_0:F
    2         4       3  float_1:F
    3         6       1  float_2:F
```

### 7.2. Float 002

```java
public class HelloWorld {
    public void testSimple() {
        float float_PI = 3.14F;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Float {Value='3.14', HexCode='044048F5C3'}
    ......

code='120244B1'
    0: ldc 2           // 1202
    2: fstore_1        // 44
    3: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       4  this:Llsieun/sample/HelloWorld;
    1         3       1  float_PI:F
```

## 8. Double

### 8.1. Double 001

```java
public class HelloWorld {
    public void testSimple() {
        double double_0 = 0;
        double double_1 = 1;
    }
}
```

```txt
code='0E480F4AB1'
    0: dconst_0        // 0E
    1: dstore_1        // 48
    2: dconst_1        // 0F
    3: dstore_3        // 4A
    4: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         2       3  double_0:D
    3         4       1  double_1:D
```

### 8.2. Double 002

```java
public class HelloWorld {
    public void testSimple() {
        double double_E = Math.E;
        double double_PI = Math.PI;
        double double_min = Double.MIN_VALUE;
        double double_max = Double.MAX_VALUE;
    }
}
```

```txt
ConstantPool {
    |003| CONSTANT_Double {Value='2.718281828459045', HexCode='064005BF0A8B145769'}
    |005| CONSTANT_Double {Value='3.141592653589793', HexCode='06400921FB54442D18'}
    |008| CONSTANT_Double {Value='4.9E-324', HexCode='060000000000000001'}
    |010| CONSTANT_Double {Value='1.7976931348623157E308', HexCode='067FEFFFFFFFFFFFFF'}
    ......

code='140003481400054A140008390514000A3907B1'
    0: ldc2_w 3        // 140003
    3: dstore_1        // 48
    4: ldc2_w 5        // 140005
    7: dstore_3        // 4A
    8: ldc2_w 8        // 140008
   11: dstore 5        // 3905
   13: ldc2_w 10       // 14000A
   16: dstore 7        // 3907
   18: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      19  this:Llsieun/sample/HelloWorld;
    1         4      15  double_E:D
    3         8      11  double_PI:D
    5        13       6  double_min:D
    7        18       1  double_max:D
```

## 9. Boxing Conversion

### 9.1. boolean to Boolean

```java
public class HelloWorld {
    public void testSimple() {
        Boolean bool_true = true;
        Boolean bool_false = false;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Methodref {Value='java/lang/Boolean.valueOf:(Z)Ljava/lang/Boolean;', HexCode='0A00130014'}
    ......

code='04B800024C03B800024DB1'
    0: iconst_1        // 04
    1: invokestatic 2  // B80002
    4: astore_1        // 4C
    5: iconst_0        // 03
    6: invokestatic 2  // B80002
    9: astore_2        // 4D
   10: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      11  this:Llsieun/sample/HelloWorld;
    1         5       6  bool_true:Ljava/lang/Boolean;
    2        10       1  bool_false:Ljava/lang/Boolean;
```

### 9.2. byte to Byte

```java
public class HelloWorld {
    public void testSimple() {
        Byte b_1 = 1;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Methodref {Value='java/lang/Byte.valueOf:(B)Ljava/lang/Byte;', HexCode='0A00120013'}
    ......

code='04B800024CB1'
    0: iconst_1        // 04
    1: invokestatic 2  // B80002
    4: astore_1        // 4C
    5: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       6  this:Llsieun/sample/HelloWorld;
    1         5       1  b_1:Ljava/lang/Byte;
```

### 9.3. short to Short

```java
public class HelloWorld {
    public void testSimple() {
        Short short_1 = 1;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Methodref {Value='java/lang/Short.valueOf:(S)Ljava/lang/Short;', HexCode='0A00120013'}
    ......

code='04B800024CB1'
    0: iconst_1        // 04
    1: invokestatic 2  // B80002
    4: astore_1        // 4C
    5: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       6  this:Llsieun/sample/HelloWorld;
    1         5       1  short_1:Ljava/lang/Short;
```

### 9.4. char to Character

```java
public class HelloWorld {
    public void testSimple() {
        Character char_a = 'a';
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Methodref {Value='java/lang/Character.valueOf:(C)Ljava/lang/Character;', HexCode='0A00120013'}
    ......

code='1061B800024CB1'
    0: bipush 97       // 1061
    2: invokestatic 2  // B80002
    5: astore_1        // 4C
    6: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       7  this:Llsieun/sample/HelloWorld;
    1         6       1  char_a:Ljava/lang/Character;
```

### 9.5. int to Integer

```java
public class HelloWorld {
    public void testSimple() {
        Integer int_62 = 62;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Methodref {Value='java/lang/Integer.valueOf:(I)Ljava/lang/Integer;', HexCode='0A00120013'}
    ......

code='103EB800024CB1'
    0: bipush 62       // 103E
    2: invokestatic 2  // B80002
    5: astore_1        // 4C
    6: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       7  this:Llsieun/sample/HelloWorld;
    1         6       1  int_62:Ljava/lang/Integer;
```

### 9.6. long to Long

```java
public class HelloWorld {
    public void testSimple() {
        Long long_618 = 618L; // 0.618是黄金分割点
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Long {Value='618', HexCode='05000000000000026A'}
    |004| CONSTANT_Methodref {Value='java/lang/Long.valueOf:(J)Ljava/lang/Long;', HexCode='0A00140015'}
    ...

code='140002B800044CB1'
    0: ldc2_w 2        // 140002
    3: invokestatic 4  // B80004
    6: astore_1        // 4C
    7: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       8  this:Llsieun/sample/HelloWorld;
    1         7       1  long_618:Ljava/lang/Long;
```

The values of the integral types are integers in the following ranges:

- For `byte` , from `-128` to `127`, inclusive
- For `short` , from `-32768` to `32767`, inclusive
- For `int` , from `-2147483648` to `2147483647`, inclusive
- For `long` , from `-9223372036854775808` to `9223372036854775807`, inclusive
- For `char` , from '`\u0000`' to '`\uffff`' inclusive, that is, from `0` to `65535`

### 9.7. float to Float

```java
public class HelloWorld {
    public void testSimple() {
        Float float_pi = 3.14F;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Float {Value='3.14', HexCode='044048F5C3'}
    |003| CONSTANT_Methodref {Value='java/lang/Float.valueOf:(F)Ljava/lang/Float;', HexCode='0A00130014'}
    ......

code='1202B800034CB1'
    0: ldc 2           // 1202
    2: invokestatic 3  // B80003
    5: astore_1        // 4C
    6: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       7  this:Llsieun/sample/HelloWorld;
    1         6       1  float_pi:Ljava/lang/Float;
```

### 9.8. double to Double

```java
public class HelloWorld {
    public void testSimple() {
        Double double_e = 2.718;
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_Double {Value='2.718', HexCode='064005BE76C8B43958'}
    |004| CONSTANT_Methodref {Value='java/lang/Double.valueOf:(D)Ljava/lang/Double;', HexCode='0A00140015'}
    ......

code='140002B800044CB1'
    0: ldc2_w 2        // 140002
    3: invokestatic 4  // B80004
    6: astore_1        // 4C
    7: return          // B1

LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       8  this:Llsieun/sample/HelloWorld;
    1         7       1  double_e:Ljava/lang/Double;
```

## 10. Unboxing Conversion

### 10.1. Boolean to boolean

```java
public class HelloWorld {
    public void testSimple() {
        boolean bool_true = Boolean.valueOf("TRUE");
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_String {Value='TRUE', HexCode='080014'}
    |003| CONSTANT_Methodref {Value='java/lang/Boolean.valueOf:(Ljava/lang/String;)Ljava/lang/Boolean;', HexCode='0A00150016'}
    |004| CONSTANT_Methodref {Value='java/lang/Boolean.booleanValue:()Z', HexCode='0A00150017'}
    ......

code='1202B80003B600043CB1'
    0: ldc 2           // 1202
    2: invokestatic 3  // B80003
    5: invokevirtual 4 // B60004
    8: istore_1        // 3C
    9: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      10  this:Llsieun/sample/HelloWorld;
    1         9       1  bool_true:Z
```

### 10.2. Integer to int

```java
public class HelloWorld {
    public void testSimple() {
        int int_value = Integer.valueOf("1024");
    }
}
```

```txt
ConstantPool {
    |002| CONSTANT_String {Value='1024', HexCode='080014'}
    |003| CONSTANT_Methodref {Value='java/lang/Integer.valueOf:(Ljava/lang/String;)Ljava/lang/Integer;', HexCode='0A00150016'}
    |004| CONSTANT_Methodref {Value='java/lang/Integer.intValue:()I', HexCode='0A00150017'}
    ......

code='1202B80003B600043CB1'
    0: ldc 2           // 1202
    2: invokestatic 3  // B80003
    5: invokevirtual 4 // B60004
    8: istore_1        // 3C
    9: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      10  this:Llsieun/sample/HelloWorld;
    1         9       1  int_value:I
```



## 14. 小结

我们究竟讲了什么？


