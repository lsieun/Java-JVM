# Opcode Transfer Values

<!-- TOC -->

- [1. int and float](#1-int-and-float)
  - [1.1. int](#11-int)
  - [1.2. float](#12-float)
- [2. long and double](#2-long-and-double)
  - [2.1. long](#21-long)
  - [2.2. double](#22-double)
- [3. Object](#3-object)
- [4. Summing-up](#4-summing-up)

<!-- /TOC -->

The `ILOAD`, `LLOAD`, `FLOAD`, `DLOAD`, and `ALOAD` instructions read a local variable and push its value on the operand stack. They take as **argument** the index `i` of the local variable that must be read.

- :one:`ILOAD` is used to load a `boolean`, `byte`, `char`, `short`, or `int` local variable.
- :two:`LLOAD`, `FLOAD` and `DLOAD` are used to load a `long`, `float` or `double` value, respectively (`LLOAD` and `DLOAD` actually load the two slots `i` and `i + 1`).
- :three:Finally `ALOAD` is used to load any **non primitive value**, i.e. **object** and **array references**. 

Symmetrically the `ISTORE`, `LSTORE`, `FSTORE`, `DSTORE` and `ASTORE` instructions pop a value from the **operand stack** and store it in a **local variable** designated by its index `i`.

As you can see the `xLOAD` and `xSTORE` instructions are **typed** (in fact, as you will see below, almost all instructions are typed). This is used to ensure that no illegal conversion is done. Indeed it is illegal to **store** a value in a local variable and then to **load** it with a different type. For instance the `ISTORE 1 ALOAD 1` sequence is illegal – it would allow to **store** an arbitrary memory address in local variable `1`, and to **convert** this address to an object reference! It is however perfectly legal to **store** in a local variable a value whose type differ from the type of the current value stored in this local variable. This means that the type of a local variable, i.e. the type of the value stored in this local variable, can change during the execution of a method.

## 1. int and float

### 1.1. int

学习目标：

- `iload_<n>`
- `istore_<n>`
- `iload`
- `istore`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int a = 1;
        int b = a;
        int c = b;
        int d = c;
        int e = d;
    }
}
```

输出结果：

```txt
code='043C1B3D1C3E1D360415043605B1'
    0: iconst_1        // 04
    1: istore_1        // 3C
    2: iload_1         // 1B
    3: istore_2        // 3D
    4: iload_2         // 1C
    5: istore_3        // 3E
    6: iload_3         // 1D
    7: istore 4        // 3604
    9: iload 4         // 1504
   11: istore 5        // 3605
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         2      12  a:I
    2         4      10  b:I
    3         6       8  c:I
    4         9       5  d:I
    5        13       1  e:I
```

### 1.2. float

学习目标：

- `fload_<n>`
- `fstore_<n>`
- `fload`
- `fstore`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        float a = 1;
        float b = a;
        float c = b;
        float d = c;
        float e = d;
    }
}
```

输出结果：

```txt
code='0C442345244625380417043805B1'
    0: fconst_1        // 0C
    1: fstore_1        // 44
    2: fload_1         // 23
    3: fstore_2        // 45
    4: fload_2         // 24
    5: fstore_3        // 46
    6: fload_3         // 25
    7: fstore 4        // 3804
    9: fload 4         // 1704
   11: fstore 5        // 3805
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         2      12  a:F
    2         4      10  b:F
    3         6       8  c:F
    4         9       5  d:F
    5        13       1  e:F
```

## 2. long and double

`LLOAD` and `DLOAD` actually load the two slots `i` and `i + 1`.这一点，应该好好说一说。

### 2.1. long

学习目标：

- `lload_<n>`
- `fstore_<n>`
- `lload`
- `fstore`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        long a = 1;
        long b = a;
        long c = b;
        long d = c;
    }
}
```

输出结果：

```txt
code='0A401F4221370516053707B1'
    0: lconst_1        // 0A
    1: lstore_1        // 40
    2: lload_1         // 1F
    3: lstore_3        // 42
    4: lload_3         // 21
    5: lstore 5        // 3705
    7: lload 5         // 1605
    9: lstore 7        // 3707
   11: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      12  this:Llsieun/sample/HelloWorld;
    1         2      10  a:J
    3         4       8  b:J
    5         7       5  c:J
    7        11       1  d:J
```

### 2.2. double

学习目标：

- `dload_<n>`
- `dstore_<n>`
- `dload`
- `dstore`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        double a = 1;
        double b = a;
        double c = b;
        double d = c;
    }
}
```

输出结果：

```txt
code='0F48274A29390518053907B1'
    0: dconst_1        // 0F
    1: dstore_1        // 48
    2: dload_1         // 27
    3: dstore_3        // 4A
    4: dload_3         // 29
    5: dstore 5        // 3905
    7: dload 5         // 1805
    9: dstore 7        // 3907
   11: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      12  this:Llsieun/sample/HelloWorld;
    1         2      10  a:D
    3         4       8  b:D
    5         7       5  c:D
    7        11       1  d:D
```

## 3. Object

学习目标：

- `aload_<n>`
- `astore_<n>`
- `aload`
- `astore`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object a = null;
        Object b = a;
        Object c = b;
        Object d = c;
        Object e = d;
    }
}
```

输出结果：

```txt
code='014C2B4D2C4E2D3A0419043A05B1'
    0: aconst_null     // 01
    1: astore_1        // 4C
    2: aload_1         // 2B
    3: astore_2        // 4D
    4: aload_2         // 2C
    5: astore_3        // 4E
    6: aload_3         // 2D
    7: astore 4        // 3A04
    9: aload 4         // 1904
   11: astore 5        // 3A05
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         2      12  a:Ljava/lang/Object;
    2         4      10  b:Ljava/lang/Object;
    3         6       8  c:Ljava/lang/Object;
    4         9       5  d:Ljava/lang/Object;
    5        13       1  e:Ljava/lang/Object;
```

## 4. Summing-up

知识总结

| opcode | mnemonic symbol | meaning                               |
| ------ | --------------- | ------------------------------------- |
| `21`   | `iload`         | Load `int` from local variable        |
| `22`   | `lload`         | Load `long` from local variable       |
| `23`   | `fload`         | Load `float` from local variable      |
| `24`   | `dload`         | Load `double` from local variable     |
| `25`   | `aload`         | Load `reference` from local variable  |
| `26`   | `iload_0`       | Load `int` from local variable        |
| `27`   | `iload_1`       |                                       |
| `28`   | `iload_2`       |                                       |
| `29`   | `iload_3`       |                                       |
| `30`   | `lload_0`       | Load `long` from local variable       |
| `31`   | `lload_1`       |                                       |
| `32`   | `lload_2`       |                                       |
| `33`   | `lload_3`       |                                       |
| `34`   | `fload_0`       | Load `float` from local variable      |
| `35`   | `fload_1`       |                                       |
| `36`   | `fload_2`       |                                       |
| `37`   | `fload_3`       |                                       |
| `38`   | `dload_0`       | Load `double` from local variable     |
| `39`   | `dload_1`       |                                       |
| `40`   | `dload_2`       |                                       |
| `41`   | `dload_3`       |                                       |
| `42`   | `aload_0`       | Load `reference` from local variable  |
| `43`   | `aload_1`       |                                       |
| `44`   | `aload_2`       |                                       |
| `45`   | `aload_3`       |                                       |
| `54`   | `istore`        | Store `int` into local variable       |
| `55`   | `lstore`        | Store `long` into local variable      |
| `56`   | `fstore`        | Store `float` into local variable     |
| `57`   | `dstore`        | Store `double` into local variable    |
| `58`   | `astore`        | Store `reference` into local variable |
| `59`   | `istore_0`      | Store `int` into local variable       |
| `60`   | `istore_1`      |                                       |
| `61`   | `istore_2`      |                                       |
| `62`   | `istore_3`      |                                       |
| `63`   | `lstore_0`      | Store `long` into local variable      |
| `64`   | `lstore_1`      |                                       |
| `65`   | `lstore_2`      |                                       |
| `66`   | `lstore_3`      |                                       |
| `67`   | `fstore_0`      | Store `float` into local variable     |
| `68`   | `fstore_1`      |                                       |
| `69`   | `fstore_2`      |                                       |
| `70`   | `fstore_3`      |                                       |
| `71`   | `dstore_0`      | Store `double` into local variable    |
| `72`   | `dstore_1`      |                                       |
| `73`   | `dstore_2`      |                                       |
| `74`   | `dstore_3`      |                                       |
| `75`   | `astore_0`      | Store `reference` into local variable |
| `76`   | `astore_1`      |                                       |
| `77`   | `astore_2`      |                                       |
| `78`   | `astore_3`      |                                       |

