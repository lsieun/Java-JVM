# Opcode Arrays

<!-- TOC -->

- [1. new array](#1-new-array)
  - [1.1. byte array](#11-byte-array)
  - [1.2. int array](#12-int-array)
  - [1.3. Object array](#13-object-array)
  - [1.4. multianewarray](#14-multianewarray)
- [2. Get and Set Array Element](#2-get-and-set-array-element)
  - [2.1. int Array Get And Set](#21-int-array-get-and-set)
  - [2.2. Object Array Get And Set](#22-object-array-get-and-set)
- [3. Array Length](#3-array-length)
  - [3.1. int Array Length](#31-int-array-length)
  - [3.2. Object Array Length](#32-object-array-length)
- [4. Summing-up](#4-summing-up)

<!-- /TOC -->

**Arrays**. These instructions are used to read and write values in arrays. The `xALOAD` instructions pop an index and an array, and push the value of the array element at this index. The `xASTORE` instructions pop a value, an index and an array, and store this value at that index in the array. Here `x` can be `I`, `L`, `F`, `D` or `A`, but also `B`, `C` or `S`.

## 1. new array

### 1.1. byte array

学习目标：

- `newarray`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        byte[] byteArray = new byte[0];
    }
}
```

输出结果：

```txt
code='03BC084CB1'
    0: iconst_0        // 03
    1: newarray 8      // BC08
    3: astore_1        // 4C
    4: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         4       1  byteArray:[B
```



Format: `newarray atype`

Array type codes

| Array Type  | atype |
| ----------- | ----- |
| `T_BOOLEAN` | `4`   |
| `T_CHAR`    | `5`   |
| `F_FLOAT`   | `6`   |
| `T_DOUBLE`  | `7`   |
| `T_BYTE`    | `8`   |
| `T_SHORT`   | `9`   |
| `T_INT`     | `10`  |
| `T_LONG`    | `11`  |



### 1.2. int array

学习目标：

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int[] intArray = new int[1];
    }
}
```

输出结果：

```txt
code='04BC0A4CB1'
    0: iconst_1        // 04
    1: newarray 10     // BC0A
    3: astore_1        // 4C
    4: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       5  this:Llsieun/sample/HelloWorld;
    1         4       1  intArray:[I
```

### 1.3. Object array

学习目标：

- `anewarray`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object[] objArray = new Object[2];
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/Object', HexCode='070011'}
    ......

code='05BD00024CB1'
    0: iconst_2        // 05
    1: anewarray 2     // BD0002
    4: astore_1        // 4C
    5: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       6  this:Llsieun/sample/HelloWorld;
    1         5       1  objArray:[Ljava/lang/Object;
```

### 1.4. multianewarray

```java
public class HelloWorld {
    public void testSimple() {
        int[][] array = new int[3][4];
    }
}
```

```txt
HexCode: 0607c50002024cb1
0000: iconst_3             // 06
0001: iconst_4             // 07
0002: multianewarray  #2   // c5000202 || [[I
0006: astore_1             // 4c
0007: return               // b1
```

```java
public class HelloWorld {
    public void testSimple() {
        Object[][] array = new Object[3][4];
    }
}
```

```txt
HexCode: 0607c50002024cb1
0000: iconst_3             // 06
0001: iconst_4             // 07
0002: multianewarray  #2   // c5000202 || [[Ljava/lang/Object;
0006: astore_1             // 4c
0007: return               // b1
```

## 2. Get and Set Array Element

### 2.1. int Array Get And Set

学习目标：

- `iastore`
- `iaload`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int[] intArray = new int[1];
        intArray[0] = 10;
        int i = intArray[0];
    }
}
```

输出结果：

```txt
code='04BC0A4C2B03100A4F2B032E3DB1'
    0: iconst_1        // 04
    1: newarray 10     // BC0A
    3: astore_1        // 4C
    4: aload_1         // 2B
    5: iconst_0        // 03
    6: bipush 10       // 100A
    8: iastore         // 4F
    9: aload_1         // 2B
   10: iconst_0        // 03
   11: iaload          // 2E
   12: istore_2        // 3D
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         4      10  intArray:[I
    2        13       1  i:I
```

### 2.2. Object Array Get And Set

学习目标：

- `aastore`
- `aaload`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object[] objArray = new Object[2];
        objArray[0] = null;
        Object obj = objArray[0];
    }
}
```

输出结果：

```txt
ConstantPool {
    |002| CONSTANT_Class {Value='java/lang/Object', HexCode='070013'}
    ......

code='05BD00024C2B0301532B03324DB1'
    0: iconst_2        // 05
    1: anewarray 2     // BD0002
    4: astore_1        // 4C
    5: aload_1         // 2B
    6: iconst_0        // 03
    7: aconst_null     // 01
    8: aastore         // 53
    9: aload_1         // 2B
   10: iconst_0        // 03
   11: aaload          // 32
   12: astore_2        // 4D
   13: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0      14  this:Llsieun/sample/HelloWorld;
    1         5       9  objArray:[Ljava/lang/Object;
    2        13       1  obj:Ljava/lang/Object;
```

## 3. Array Length

### 3.1. int Array Length

学习目标：

- `arraylength`

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        int[] intArray = new int[1];
        int length = intArray.length;
    }
}
```

输出结果：

```txt
code='04BC0A4C2BBE3DB1'
    0: iconst_1        // 04
    1: newarray 10     // BC0A
    3: astore_1        // 4C
    4: aload_1         // 2B
    5: arraylength     // BE
    6: istore_2        // 3D
    7: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       8  this:Llsieun/sample/HelloWorld;
    1         4       4  intArray:[I
    2         7       1  length:I
```

### 3.2. Object Array Length

学习目标：

- `arraylength`是一个通用的opcode，适用于Object Array。

示例代码：

```java
public class HelloWorld {
    public void testSimple() {
        Object[] objArray = new Object[2];
        int length = objArray.length;
    }
}
```

输出结果：

```txt
ConstantPool {
    |001| CONSTANT_Methodref {Value='java/lang/Object.<init>:()V', HexCode='0A00020012'}
    |002| CONSTANT_Class {Value='java/lang/Object', HexCode='070013'}
    ......

code='05BD00024C2BBE3DB1'
    0: iconst_2        // 05
    1: anewarray 2     // BD0002
    4: astore_1        // 4C
    5: aload_1         // 2B
    6: arraylength     // BE
    7: istore_2        // 3D
    8: return          // B1


LocalVariableTable:
index  start_pc  length  name_and_type
    0         0       9  this:Llsieun/sample/HelloWorld;
    1         5       4  objArray:[Ljava/lang/Object;
    2         8       1  length:I
```

## 4. Summing-up

知识整理

| opcode | mnemonic symbol | meaning                              |
| ------ | --------------- | ------------------------------------ |
| `46`   | `iaload`        | Load `int` from array                |
| `47`   | `laload`        |                                      |
| `48`   | `faload`        |                                      |
| `49`   | `daload`        |                                      |
| `50`   | `aaload`        | Load `reference` from array          |
| `51`   | `baload`        | Load `byte` or `boolean` from array  |
| `52`   | `caload`        |                                      |
| `53`   | `saload`        |                                      |
| `79`   | `iastore`       | Store into `int` array               |
| `80`   | `lastore`       |                                      |
| `81`   | `fastore`       |                                      |
| `82`   | `dastore`       |                                      |
| `83`   | `aastore`       | Store into `reference` array         |
| `84`   | `bastore`       | Store into `byte` or `boolean` array |
| `85`   | `castore`       |                                      |
| `86`   | `sastore`       |                                      |
| `188`  | `newarray`      | Create new array                     |
| `189`  | `anewarray`     | Create new array of `reference`      |
| `190`  | `arraylength`   | Get length of array                  |

